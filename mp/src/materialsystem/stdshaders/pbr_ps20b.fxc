//==================================================================================================
//
// Physically Based Rendering pixel shader for brushes and models
//
//==================================================================================================

// STATIC: "FLASHLIGHT"                 "0..1"
// STATIC: "FLASHLIGHTDEPTHFILTERMODE"  "0..2"
// STATIC: "LIGHTMAPPED"                "0..1"
// STATIC: "EMISSIVE"                   "0..1"
// STATIC: "WVT"                        "0..1"
// STATIC: "HSV"                        "0..1"
// STATIC: "HSV_BLEND"                  "0..1"

// DYNAMIC: "WRITEWATERFOGTODESTALPHA"  "0..1"
// DYNAMIC: "PIXELFOGTYPE"              "0..1"
// DYNAMIC: "NUM_LIGHTS"                "0..4"
// DYNAMIC: "WRITE_DEPTH_TO_DESTALPHA"  "0..1"
// DYNAMIC: "FLASHLIGHTSHADOWS"         "0..1"

// Can't write fog to alpha if there is no fog
// SKIP: ($PIXELFOGTYPE == 0) && ($WRITEWATERFOGTODESTALPHA != 0)
// We don't care about flashlight depth unless the flashlight is on
// SKIP: ( $FLASHLIGHT == 0 ) && ( $FLASHLIGHTSHADOWS == 1 )
// Flashlight shadow filter mode is irrelevant if there is no flashlight
// SKIP: ( $FLASHLIGHT == 0 ) && ( $FLASHLIGHTDEPTHFILTERMODE != 0 )

// SKIP: !$HSV && $HSV_BLEND

#include "common_ps_fxc.h"
#include "common_flashlight_fxc.h"
#include "common_lightmappedgeneric_fxc.h"
#include "shader_constant_register_map.h"

#include "pbr_common_ps2_3_x.h"

const float4 g_DiffuseModulation                : register(PSREG_DIFFUSE_MODULATION);
const float4 g_ShadowTweaks                     : register(PSREG_ENVMAP_TINT__SHADOW_TWEAKS);
const float3 cAmbientCube[6]                    : register(PSREG_AMBIENT_CUBE);
const float4 g_EyePos                           : register(PSREG_EYEPOS_SPEC_EXPONENT);
const float4 g_FogParams                        : register(PSREG_FOG_PARAMS);
const float4 g_FlashlightAttenuationFactors     : register(PSREG_FLASHLIGHT_ATTENUATION);
const float4 g_FlashlightPos                    : register(PSREG_FLASHLIGHT_POSITION_RIM_BOOST);
const float4x4 g_FlashlightWorldToTexture       : register(PSREG_FLASHLIGHT_TO_WORLD_TEXTURE);
PixelShaderLightInfo cLightInfo[3]              : register(PSREG_LIGHT_INFO_ARRAY);         // 2 registers each - 6 registers total (4th light spread across w's)
const float4 g_HSV                              : register(PSREG_SELFILLUMTINT);
const float4 g_EmissionScale                    : register(c2);
const float4 g_MraoScale                        : register(c27);

#define lightMapScale g_HSV.w

sampler BaseTextureSampler          : register(s0);     // Base map, selfillum in alpha
sampler NormalTextureSampler        : register(s1);     // Normal map
sampler EnvmapSampler               : register(s2);     // Cubemap
sampler ShadowDepthSampler          : register(s4);     // Flashlight shadow depth map sampler
sampler RandRotSampler              : register(s5);     // RandomRotation sampler
sampler FlashlightSampler           : register(s6);     // Flashlight cookie 
sampler LightmapSampler             : register(s7);     // Lightmap
sampler MRAOTextureSampler          : register(s10);    // MRAO texture
#if EMISSIVE
sampler EmissionTextureSampler      : register(s11);    // Emission texture
#endif

#if WVT
const float4 g_Emission2Scale       : register(c3);
const float4 g_Mrao2Scale           : register(c10);
sampler Base2TextureSampler         : register(s3);
sampler Normal2TextureSampler       : register(s12);
sampler MRAO2TextureSampler         : register(s13);
#if EMISSIVE
sampler Emission2TextureSampler     : register(s14);
#endif
#endif

#define ENVMAPLOD (g_EyePos.a)

struct PS_INPUT
{
    float2 baseTexCoord             : TEXCOORD0;
    float4 lightAtten               : TEXCOORD1;
    float3 worldNormal              : TEXCOORD2;
    float3 worldTangent             : TEXCOORD3;
    float3 worldPos                 : TEXCOORD4;
    float3 projPos                  : TEXCOORD5;
    float4 lightmapTexCoord1And2    : TEXCOORD6;
    float4 lightmapTexCoord3        : TEXCOORD7;
#if WVT
    float blend                     : COLOR0;
#endif
};

// Entry point
float4 main(PS_INPUT i) : COLOR
{
    float2 correctedTexCoord = i.baseTexCoord;

#if WVT
    float4 albedo = lerp(tex2D(BaseTextureSampler, correctedTexCoord), tex2D(Base2TextureSampler, correctedTexCoord), i.blend);
#else
    float4 albedo = tex2D(BaseTextureSampler, correctedTexCoord);
#endif
    albedo.xyz *= g_DiffuseModulation;

#if WVT
    float4 mrao = lerp(tex2D(MRAOTextureSampler, correctedTexCoord) * float4(g_MraoScale.xyz, 1), tex2D(MRAO2TextureSampler, correctedTexCoord) * float4(g_Mrao2Scale.xyz, 1), i.blend);
#else
    float4 mrao = tex2D(MRAOTextureSampler, correctedTexCoord) * float4(g_MraoScale.xyz, 1);
#endif
    float metalness = mrao.x, roughness = mrao.y, ambientOcclusion = mrao.z;

#if EMISSIVE
#if WVT
    float3 emission = lerp(tex2D(EmissionTextureSampler, correctedTexCoord).xyz * g_EmissionScale.xyz, tex2D(Emission2TextureSampler, correctedTexCoord).xyz * g_Emission2Scale.xyz, i.blend);
#else
    float3 emission = tex2D(EmissionTextureSampler, correctedTexCoord).xyz * g_EmissionScale.xyz;
#endif
#endif

#if HSV
    float3 albedoHSV = rgb2hsv(albedo.rgb);
    albedoHSV.x = fmod(albedoHSV.x + g_HSV.x, 1.0);
    albedoHSV.yz = clamp(albedoHSV.yz * g_HSV.yz, 0.0, 1.0);
#if !HSV_BLEND
    albedo.xyz = hsv2rgb(albedoHSV);
#else
    albedo.xyz = lerp(albedo.xyz, hsv2rgb(albedoHSV), mrao.w);
#endif
#endif

    float3x3 normalBasis = float3x3(i.worldTangent, cross(i.worldNormal, i.worldTangent), i.worldNormal);
#if WVT
    float3 textureNormal = normalize((lerp(tex2D( NormalTextureSampler, correctedTexCoord).xyz, tex2D( Normal2TextureSampler, correctedTexCoord).xyz, i.blend) - float3(0.5, 0.5, 0.5)) * 2);
#else
    float3 textureNormal = normalize((tex2D( NormalTextureSampler, correctedTexCoord).xyz - float3(0.5, 0.5, 0.5)) * 2);
#endif
    float3 normal = normalize(mul(textureNormal, normalBasis)); // World Normal

    float3 outgoingLightDirection = normalize(g_EyePos.xyz - i.worldPos); // Lo
    float lightDirectionAngle = max(0, dot(normal, outgoingLightDirection)); // cosLo

    float3 specularReflectionVector = 2.0 * lightDirectionAngle * normal - outgoingLightDirection; // Lr

    float3 dielectricCoefficient = 0.04; // F0 dielectric
    float3 fresnelReflectance = lerp(dielectricCoefficient, albedo.rgb, metalness); // F0

    // Prepare ambient and direct
    float3 ambientLighting = 0.0;
    float3 directLighting = 0.0;

#if !FLASHLIGHT

    float4 modul = g_DiffuseModulation;
#if HSV
    float3 modulHSV = rgb2hsv(modul.rgb);
    modulHSV.x = fmod(modulHSV.x + g_HSV.x, 1.0);
    modulHSV.yz = clamp(modulHSV.yz * g_HSV.yz, 0.0, 1.0);
#if !HSV_BLEND
    modul.xyz = hsv2rgb(modulHSV);
#else
    modul.xyz = lerp(modul.xyz, hsv2rgb(modulHSV), mrao.w);
#endif
#endif

#ifdef LIGHTMAPPED
    modul *= lightMapScale;
#endif

    // Start ambient
    float3 diffuseIrradiance = ambientLookup(normal, cAmbientCube, textureNormal, i.lightmapTexCoord1And2, i.lightmapTexCoord3, LightmapSampler, modul);
    float3 ambientLightingFresnelTerm = fresnelSchlick(fresnelReflectance, lightDirectionAngle); // F

    float3 diffuseContributionFactor = lerp(1 - ambientLightingFresnelTerm, 0, metalness); ; // kd
    float3 diffuseIBL = diffuseContributionFactor * albedo.rgb * diffuseIrradiance;

    float4 specularUV = float4(specularReflectionVector, roughness * ENVMAPLOD);
    float3 lookupHigh = ENV_MAP_SCALE * texCUBE(EnvmapSampler, specularUV.xyz).xyz;

    float3 lookupLow = PixelShaderAmbientLight(specularReflectionVector, cAmbientCube);
    float3 specularIrradiance = lerp(lookupHigh, lookupLow, roughness * roughness);
    float3 specularIBL = specularIrradiance * EnvBRDFApprox(fresnelReflectance, roughness, lightDirectionAngle);

    ambientLighting = (diffuseIBL + specularIBL) * ambientOcclusion;
    // End ambient

    // Start direct
    for (int n = 0; n < NUM_LIGHTS; ++n)
    {
        float3 LightIn = normalize(PixelShaderGetLightVector(i.worldPos, cLightInfo, n));
        float3 LightColor = PixelShaderGetLightColor(cLightInfo, n) * GetAttenForLight(i.lightAtten, n); // Li

        directLighting += calculateLight(LightIn, LightColor, outgoingLightDirection,
            normal, fresnelReflectance, roughness, metalness, lightDirectionAngle, albedo.rgb);
    }
    // End direct

#else

    // Start flashlight
    float4 flashlightSpacePosition = mul(float4(i.worldPos, 1.0), g_FlashlightWorldToTexture);
    clip( flashlightSpacePosition.w ); // Stop projected textures from projecting backwards (only really happens if they have a big FOV because they get frustum culled)
    float3 vProjCoords = flashlightSpacePosition.xyz / flashlightSpacePosition.w;

    float3 delta = g_FlashlightPos.xyz - i.worldPos;
    float distSquared = dot(delta, delta);
    float dist = sqrt(distSquared);

    float3 flashlightColor = tex2D(FlashlightSampler, vProjCoords.xy);
    flashlightColor *= cFlashlightColor.xyz;

#if FLASHLIGHTSHADOWS
    float flashlightShadow = DoFlashlightShadow(ShadowDepthSampler, RandRotSampler, vProjCoords, i.projPos, FLASHLIGHTDEPTHFILTERMODE, g_ShadowTweaks, true);
    float flashlightAttenuated = lerp(flashlightShadow, 1.0, g_ShadowTweaks.y);         // Blend between fully attenuated and not attenuated
    float fAtten = saturate(dot(g_FlashlightAttenuationFactors.xyz, float3(1.0, 1.0 / dist, 1.0 / distSquared)));
    flashlightShadow = saturate(lerp(flashlightAttenuated, flashlightShadow, fAtten));  // Blend between shadow and above, according to light attenuation

    flashlightColor *= flashlightShadow;
#endif // FLASHLIGHTSHADOWS

    float farZ = g_FlashlightAttenuationFactors.w;
    float endFalloffFactor = RemapValClamped(dist, farZ, 0.6 * farZ, 0.0, 1.0);

    float3 flashLightIntensity = flashlightColor * endFalloffFactor;
        
    float3 flashLightIn = normalize(g_FlashlightPos.xyz - i.worldPos);

    directLighting += max(0, calculateLight(flashLightIn, flashLightIntensity, outgoingLightDirection,
        normal, fresnelReflectance, roughness, metalness, lightDirectionAngle, albedo.rgb));
    // End flashlight

#endif // !FLASHLIGHT
    
    float fogFactor = CalcPixelFogFactor(PIXELFOGTYPE, g_FogParams, g_EyePos.xyz, i.worldPos.xyz, i.projPos.z);

#if !FLASHLIGHT

#if WRITEWATERFOGTODESTALPHA && (PIXELFOGTYPE == PIXEL_FOG_TYPE_HEIGHT)
    float alpha = fogFactor;
#else
    float alpha = albedo.a;
#endif // WRITEWATERFOGTODESTALPHA && (PIXELFOGTYPE == PIXEL_FOG_TYPE_HEIGHT)

#else

    float alpha = 0.0f;

#endif // !FLASHLIGHT

    bool bWriteDepthToAlpha = (WRITE_DEPTH_TO_DESTALPHA != 0) && (WRITEWATERFOGTODESTALPHA == 0);

    float3 combinedLighting = directLighting + ambientLighting;

#if EMISSIVE && !FLASHLIGHT
    combinedLighting += emission;
#endif

    alpha *= g_DiffuseModulation.a;
    return FinalOutput(float4(combinedLighting, alpha), fogFactor, PIXELFOGTYPE, TONEMAP_SCALE_LINEAR, bWriteDepthToAlpha, i.projPos.z);
}
